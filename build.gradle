buildscript {
    ext.kotlin_version = '1.3.20'
    ext.isTravis = System.getenv("TRAVIS") == "true"
    ext.travisBuildNumber = System.getenv("TRAVIS_BUILD_NUMBER")
    ext.isReleaseVersion = !isTravis
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'net.researchgate.release' version '2.7.0'
    id 'signing'
}

allprojects {
    apply plugin: 'kotlin'
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'signing'
    apply plugin: 'maven-publish'

    sourceCompatibility = 1.6
    targetCompatibility = 1.6

    group = "io.kotlintest"

    if (isTravis) {
        version "3.3.0." + travisBuildNumber + "-SNAPSHOT"
    }

    repositories {
        mavenCentral()
    }

    publishing {
        repositories {
            maven {

                def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"

                name 'deploy'
                url = isTravis ? releasesRepoUrl : snapshotsRepoUrl
                credentials {
                    username = System.getenv("OSSRH_USERNAME") ?: ossrhUsername
                    password = System.getenv("OSSRH_PASSWORD") ?: ossrhPassword
                }
            }
        }
        publications {
            mavenJava(MavenPublication) {

                from components.java

                if (isReleaseVersion) {
                    artifact sourcesJar
                    artifact javadocJar
                }

                pom {
                    name = 'KotlinTest'
                    description = 'Kotlin Test Framework'
                    url = 'http://www.github.com/kotlintest/kotlintest'

                    scm {
                        connection = 'scm:git:http://www.github.com/kotlintest/kotlintest/'
                        developerConnection = 'scm:git:http://github.com/sksamuel/'
                        url = 'http://www.github.com/kotlintest/kotlintest/'
                    }

                    licenses {
                        license {
                            name = 'The Apache 2.0 License'
                            url = 'https://opensource.org/licenses/Apache-2.0'
                        }
                    }

                    developers {
                        developer {
                            id = 'sksamuel'
                            name = 'Stephen Samuel'
                            email = 'sam@sksamuel.com'
                        }
                    }
                }
            }
        }
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib"
    }

    release {
        failOnPublishNeeded = false
    }

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives javadocJar, sourcesJar
    }

    build {
        afterReleaseBuild.dependsOn publish
    }

    signing {
        useGpgCmd()
        sign configurations.archives
    }

    tasks.withType(Sign) {
        onlyIf { isReleaseVersion }
    }

    tasks.withType(Javadoc) {
        onlyIf { isReleaseVersion }
    }
}

project(":") {
    uploadArchives.enabled = false
}