buildscript {
    ext.kotlin_version = '1.2.30'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'net.researchgate.release' version '2.6.0'
}

allprojects {

    apply plugin: 'kotlin'
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'signing'
    apply plugin: "jacoco"

    group = "io.kotlintest"

    sourceCompatibility = 1.6
    targetCompatibility = 1.6

    repositories {
        mavenCentral()
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
        compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    }

    release {
        failOnPublishNeeded = false
    }

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    test {
        useJUnitPlatform()

        // show standard out and standard error of the test JVM(s) on the console
        testLogging.showStandardStreams = true

        // listen to events in the test execution lifecycle
        beforeTest { descriptor ->
            logger.lifecycle("Running test: " + descriptor)
        }

        testLogging {
            events "PASSED", "STARTED", "FAILED", "SKIPPED"
        }
    }

    artifacts {
        archives javadocJar, sourcesJar
    }

    signing {
        sign configurations.archives
    }

    build {
        afterReleaseBuild.dependsOn uploadArchives
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project {
                    name 'KotlinTest'
                    packaging 'jar'
                    description 'Kotlin Test Framework'
                    url 'http://www.github.com/kotlintest/kotlintest'

                    scm {
                        connection 'scm:git:http://www.github.com/kotlintest/kotlintest/'
                        developerConnection 'scm:git:http://github.com/sksamuel/'
                        url 'http://www.github.com/kotlintest/kotlintest/'
                    }

                    licenses {
                        license {
                            name 'The Apache 2.0 License'
                            url 'https://opensource.org/licenses/Apache-2.0'
                        }
                    }

                    developers {
                        developer {
                            id 'sksamuel'
                            name 'Stephen Samuel'
                            email 'sam@sksamuel.com'
                        }
                    }
                }
            }
        }
    }

    jacocoTestReport {
        reports {
            xml.enabled true
            xml.destination "${buildDir}/reports/jacoco/report.xml"
            csv.enabled false
            html.destination "${buildDir}/jacocoHtml"
        }
    }
}

