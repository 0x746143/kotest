buildscript {
    ext.kotlin_version = "1.1.0"
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

group = "io.kotlintest"
version = "2.0.0-SNAPSHOT"

apply plugin: 'kotlin'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: "jacoco"

repositories {
    mavenCentral()
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

dependencies {
    compile 'com.google.guava:guava:18.0'
    compile 'com.sksamuel.koors:koors:0.90.0'
    compile 'org.jetbrains.kotlin:kotlin-stdlib:1.1.0'
    compile 'org.jetbrains.kotlin:kotlin-reflect:1.1.0'
    compile 'org.mockito:mockito-core:2.7.11'
    compile 'org.reflections:reflections:0.9.10'
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'KotlinTest'
                packaging 'jar'
                description 'Kotlin Test Framework'
                url 'http://www.github.com/kotlintest/kotlintest'

                scm {
                    connection 'scm:git:http://www.github.com/kotlintest/kotlintest/'
                    developerConnection 'scm:git:http://github.com/sksamuel/'
                    url 'http://www.github.com/kotlintest/kotlintest/'
                }

                licenses {
                    license {
                        name 'The MIT License'
                        url 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id 'sksamuel'
                        name 'Stephen Samuel'
                        email 'sam@sksamuel.com'
                    }
                }
            }
        }
    }
}
sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
}

jacocoTestReport {
    reports {
        xml.enabled true
        xml.destination "${buildDir}/reports/jacoco/report.xml"
        csv.enabled false
        html.destination "${buildDir}/jacocoHtml"
    }
}

check.dependsOn jacocoTestReport